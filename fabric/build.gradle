plugins {
    id 'fabric-loom' version '0.6-SNAPSHOT'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

group = 'com.refinedmods.refinedstorage2'
archivesBaseName = 'refinedstorage2-fabric'
version = '2.0.0-milestone.1.0'

if (System.getenv('GITHUB_SHA') != null) {
    version += '+' + System.getenv('GITHUB_SHA').substring(0, 7)
}

repositories {
    mavenCentral()
    jcenter()
    // Required for libblockattributes
    maven {
        name = "BuildCraft"
        url = "https://mod-buildcraft.com/maven"
        content {
            includeGroup "alexiil.mc.lib"
        }
    }
    maven {
        url 'https://maven.blamejared.com'
    }
}

dependencies {
    minecraft 'com.mojang:minecraft:1.16.5'
    mappings 'net.fabricmc:yarn:1.16.5+build.6:v2'
    modImplementation "net.fabricmc:fabric-loader:0.11.3"
    modImplementation "net.fabricmc.fabric-api:fabric-api:0.32.5+1.16"
    implementation project(':core')
    modImplementation "alexiil.mc.lib:libblockattributes-all:0.8.8"
    modApi("me.sargunvohra.mcmods:autoconfig1u:3.3.1") {
        exclude(group: "net.fabricmc.fabric-api")
    }
    modApi("me.shedaniel.cloth:config-2:4.8.3") {
        exclude(group: "net.fabricmc.fabric-api")
    }
    modImplementation "me.shedaniel:RoughlyEnoughItems:5.8.9"
    modImplementation "io.github.prospector:modmenu:1.15.0+build.1"
    modImplementation "vazkii.patchouli:Patchouli:1.16.4-50-FABRIC"
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    testCompile 'org.assertj:assertj-core:3.19.0'
}

test {
    useJUnitPlatform()
}

sourceSets {
    main.resources.srcDirs += 'src/generated/resources'
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
    filesMatching("data/refinedstorage2/patchouli_books/book/book.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
}

java {
    withSourcesJar()
}

jar {
    from "../LICENSE.md"
    baseName "refinedstorage2-fabric"
}
