plugins {
    id 'fabric-loom' version '0.5-SNAPSHOT'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

group = 'com.refinedmods.refinedstorage2'
archivesBaseName = 'fabric'
version = '2.0.0-SNAPSHOT'

repositories {
    mavenCentral()
    jcenter()
    // Required for libblockattributes
    maven {
        name = "BuildCraft"
        url = "https://mod-buildcraft.com/maven"
        content {
            includeGroup "alexiil.mc.lib"
        }
    }
}

dependencies {
    implementation 'com.google.code.findbugs:jsr305:3.0.2'
    minecraft 'com.mojang:minecraft:1.16.5'
    mappings 'net.fabricmc:yarn:1.16.5+build.1:v2'
    modImplementation "net.fabricmc:fabric-loader:0.11.0"
    modImplementation "net.fabricmc.fabric-api:fabric-api:0.29.3+1.16"
    implementation project(':core')
    modImplementation "alexiil.mc.lib:libblockattributes-all:0.8.5"
    modApi("me.sargunvohra.mcmods:autoconfig1u:3.3.1") {
        exclude(group: "net.fabricmc.fabric-api")
    }
    modApi("me.shedaniel.cloth:config-2:4.8.3") {
        exclude(group: "net.fabricmc.fabric-api")
    }
    modRuntime "me.shedaniel:RoughlyEnoughItems:5.7.0"
    modImplementation "io.github.prospector:modmenu:1.14.6+build.31"
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    testCompile 'org.assertj:assertj-core:3.17.2'
}

test {
    useJUnitPlatform()
}

sourceSets {
    main.resources.srcDirs += 'src/generated/resources'
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource
}

jar {
    from "../LICENSE.md"
}
