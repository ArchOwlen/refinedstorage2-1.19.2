plugins {
    id 'java'
    id 'jacoco'
    id 'info.solidsoft.pitest' version '1.5.1'
}

sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

group = 'com.refinedmods.refinedstorage2'
archivesBaseName = 'refinedstorage2-core'
version = '2.0.0-milestone.1.1'

if (System.getenv('GITHUB_SHA') != null) {
    version += '+' + System.getenv('GITHUB_SHA').substring(0, 7)
}

repositories {
    mavenCentral()
}

dependencies {
    /* Use the same Log4j API as MC to not break logging in the development environment. */
    implementation 'org.apache.logging.log4j:log4j-api:2.14.1'
    testImplementation 'org.apache.logging.log4j:log4j-core:2.14.1'
    implementation 'com.google.guava:guava:30.1.1-jre'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.2'
    testImplementation 'nl.jqno.equalsverifier:equalsverifier:3.6'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.2'
    testImplementation 'org.assertj:assertj-core:3.20.2'
    testImplementation 'org.mockito:mockito-core:3.11.2'
    testImplementation "org.junit.jupiter:junit-jupiter-params:5.7.2"
    testImplementation "org.pitest:pitest-junit5-plugin:0.12"
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport // report is always generated after tests run
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = 'UTF-8'
    it.options.release = 16
}

jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
}

pitest {
    targetClasses = ['com.refinedmods.refinedstorage2.core.*']
    threads = 4
    outputFormats = ['HTML']
    timestampedReports = false
    junit5PluginVersion = '0.12'
}
