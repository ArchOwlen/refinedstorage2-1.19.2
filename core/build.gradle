plugins {
    id 'fabric-loom' version '0.5-SNAPSHOT'
    id 'jacoco'
    id 'info.solidsoft.pitest' version '1.5.1'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

group = 'com.refinedmods.refinedstorage2'
archivesBaseName = 'refinedstorage2-core'
version = '2.0.0-M1-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    minecraft 'com.mojang:minecraft:1.16.5'
    mappings 'net.fabricmc:yarn:1.16.5+build.5:v2'
    modImplementation "net.fabricmc:fabric-loader:0.11.2"
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    testCompile 'org.assertj:assertj-core:3.17.2'
    testCompile 'org.mockito:mockito-core:2.1.0'
    testCompile "org.junit.jupiter:junit-jupiter-params:5.7.0"
    testCompile "org.pitest:pitest-junit5-plugin:0.12"
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport // report is always generated after tests run
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
}

test {
    jacoco {
        excludes = ['net.minecraft.*']
    }
}

pitest {
    targetClasses = ['com.refinedmods.refinedstorage2.core.*']
    threads = 4
    outputFormats = ['HTML']
    timestampedReports = false
    junit5PluginVersion = '0.12'
}
